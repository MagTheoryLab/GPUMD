/*
    Copyright 2017 Zheyong Fan and GPUMD development team
    This file is part of GPUMD.
    GPUMD is free software: you can redistribute it and/or modify
    it under the terms of the GNU General Public License as published by
    the Free Software Foundation, either version 3 of the License, or
    (at your option) any later version.
    GPUMD is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU General Public License for more details.
    You should have received a copy of the GNU General Public License
    along with GPUMD.  If not, see <http://www.gnu.org/licenses/>.
*/

/*----------------------------------------------------------------------------80
Use the separable natural evolution strategy (SNES) to fit potential parameters.

Reference:

T. Schaul, T. Glasmachers, and J. Schmidhuber,
High Dimensions and Heavy Tails for Natural Evolution Strategies,
https://doi.org/10.1145/2001576.2001692
------------------------------------------------------------------------------*/

#include "fitness.cuh"
#include "parameters.cuh"
#include "snes.cuh"
#include "utilities/error.cuh"
#include "utilities/gpu_macro.cuh"
#include <algorithm>
#include "utilities/nep_parameters.cuh"
#include <chrono>
#include <cmath>
#include <iostream>
#include <cstring>

static __global__ void initialize_curand_states(gpurandState* state, int N, int seed)
{
  int n = blockIdx.x * blockDim.x + threadIdx.x;
  if (n < N) {
    gpurand_init(seed, n, 0, &state[n]);
  }
}

SNES::SNES(Parameters& para, Fitness* fitness_function)
{
  maximum_generation = para.maximum_generation;
  number_of_variables = para.number_of_variables;
  population_size = para.population_size;
  const int N = population_size * number_of_variables;
  int num = number_of_variables / para.num_types;
  eta_sigma = (3.0f + std::log(num * 1.0f)) / (5.0f * sqrt(num * 1.0f)) / 2.0f;
  fitness_total.resize(population_size * (para.num_types + 1));
  fitness_L1.resize(population_size * (para.num_types + 1));
  fitness_L2.resize(population_size * (para.num_types + 1));
  fitness_energy.resize(population_size * (para.num_types + 1));
  fitness_force.resize(population_size * (para.num_types + 1));
  fitness_virial.resize(population_size * (para.num_types + 1));
  fitness_charge.resize(population_size * (para.num_types + 1));
  fitness_bec.resize(population_size * (para.num_types + 1));
  fitness_mforce.resize(population_size * (para.num_types + 1));
  fitness_tau.resize(population_size * (para.num_types + 1));
  fitness_spin_response.resize(population_size * (para.num_types + 1));
  index.resize(population_size * (para.num_types + 1));
  population.resize(N);
  mu.resize(number_of_variables);
  sigma.resize(number_of_variables);
  cost_L1reg.resize(population_size);
  cost_L2reg.resize(population_size);
  utility.resize(population_size);
  type_of_variable.resize(number_of_variables, para.num_types);
  curriculum_parameter.resize(number_of_variables, 0);
  initialize_rng();

  gpuSetDevice(0); // normally use GPU-0
  gpu_type_of_variable.resize(number_of_variables);
  gpu_curriculum_parameter.resize(number_of_variables);
  gpu_index.resize(population_size * (para.num_types + 1));
  gpu_utility.resize(population_size);
  gpu_sigma.resize(number_of_variables);
  gpu_mu.resize(number_of_variables);
  gpu_cost_L1reg.resize(population_size);
  gpu_cost_L2reg.resize(population_size);
  gpu_s.resize(N);
  gpu_population.resize(N);
  curand_states.resize(N);
  initialize_curand_states<<<(N - 1) / 128 + 1, 128>>>(curand_states.data(), N, 1234567);
  GPU_CHECK_KERNEL

  if (para.fine_tune) {
    initialize_mu_and_sigma_fine_tune(para);
  } else {
    initialize_mu_and_sigma(para);
  }
  fitness_function->initialize_q_scaler(
    para, para.spin_mode == 3 ? mu.data() : nullptr);

  calculate_utility();
  gpu_utility.copy_from_host(utility.data());
  find_type_of_variable(para);
  if (curriculum_enabled) {
    for (int type = 0; type < para.num_types; ++type) {
      const int ann_offset = type * para.number_of_variables_ann_1;
      for (int neuron = 0; neuron < para.num_neurons1; ++neuron) {
        for (int descriptor = para.spin_order3_descriptor_start;
             descriptor < para.dim;
             ++descriptor) {
          curriculum_parameter[
            ann_offset + neuron * para.dim + descriptor] = 1;
        }
      }
    }
  }
  gpu_curriculum_parameter.copy_from_host(curriculum_parameter.data());
  gpu_type_of_variable.copy_from_host(type_of_variable.data());

  compute(para, fitness_function);
}

void SNES::initialize_rng()
{
#ifdef DEBUG
  rng = std::mt19937(12345678);
#else
  rng = std::mt19937(std::chrono::system_clock::now().time_since_epoch().count());
#endif
};

void SNES::initialize_mu_and_sigma(Parameters& para)
{
  curriculum_enabled = para.spin_curriculum;
  FILE* fid_restart = fopen("nep.restart", "r");
  if (fid_restart == NULL) {
    if (para.spin_mode == 3) {
      std::normal_distribution<float> normal(0.0f, 1.0f);
      const float input_scale =
        1.0f / std::sqrt(float(para.dim + para.num_neurons1));
      const float output_scale =
        1.0f / std::sqrt(float(para.num_neurons1 + 1));
      const float descriptor_scale = 0.1f;
      const float spin_noise = 0.01f;

      std::fill(mu.begin(), mu.end(), 0.0f);
      std::fill(sigma.begin(), sigma.end(), para.sigma0 * descriptor_scale);
      for (int type = 0; type < para.num_types; ++type) {
        const int ann_offset = type * para.number_of_variables_ann_1;
        for (int neuron = 0; neuron < para.num_neurons1; ++neuron) {
          for (int descriptor = 0; descriptor < para.dim; ++descriptor) {
            const int index =
              ann_offset + neuron * para.dim + descriptor;
            mu[index] = normal(rng) * input_scale;
            sigma[index] = para.sigma0 * input_scale;
          }
        }
        const int bias_offset =
          ann_offset + para.num_neurons1 * para.dim;
        const int output_offset = bias_offset + para.num_neurons1;
        for (int neuron = 0; neuron < para.num_neurons1; ++neuron) {
          mu[bias_offset + neuron] = 0.0f;
          sigma[bias_offset + neuron] = para.sigma0 * input_scale;
          mu[output_offset + neuron] = normal(rng) * output_scale;
          sigma[output_offset + neuron] = para.sigma0 * output_scale;
        }
      }
      const int descriptor_offset = para.number_of_variables_ann;
      const int type_pairs = para.num_types * para.num_types;
      const int radial_basis_count = para.basis_size_radial + 1;
      const int radial_channel_count = para.n_max_radial + 1;
      const int radial_count =
        type_pairs * radial_channel_count * radial_basis_count;
      for (int channel = 0; channel < radial_channel_count; ++channel) {
        for (int basis = 0; basis < radial_basis_count; ++basis) {
          for (int pair = 0; pair < type_pairs; ++pair) {
            const int index = descriptor_offset +
              pair * radial_channel_count * radial_basis_count +
              channel * radial_basis_count + basis;
            mu[index] = normal(rng) * spin_noise;
            if (basis == channel % radial_basis_count) {
              mu[index] += 1.0f;
            }
          }
        }
      }
      const int angular_basis_count = para.basis_size_angular + 1;
      const int angular_channel_count = para.n_max_angular + 1;
      const int angular_offset = descriptor_offset + radial_count;
      for (int channel = 0; channel < angular_channel_count; ++channel) {
        for (int basis = 0; basis < angular_basis_count; ++basis) {
          for (int pair = 0; pair < type_pairs; ++pair) {
            const int index = angular_offset +
              pair * angular_channel_count * angular_basis_count +
              channel * angular_basis_count + basis;
            mu[index] = normal(rng) * spin_noise;
            if (basis == channel % angular_basis_count) {
              mu[index] += 1.0f;
            }
          }
        }
      }

      const int structural_count = radial_count +
        type_pairs * angular_channel_count * angular_basis_count;

      const int spin_offset = descriptor_offset + structural_count;
      const int basis_count = para.spin_basis_size[0] + 1;
      // Least-squares coefficients of x^c f_c(x) in the fixed B8 magnetic
      // Chebyshev basis, sampled exactly as the Spin3 TorchNEP initializer.
      static constexpr float spin3_radial_frame[9][9] = {
        {1.0000000000e+00f, 0.0f, 0.0f, 0.0f, 0.0f, 0.0f, 0.0f, 0.0f, 0.0f},
        {7.2002207902e-01f, -8.3611353484e-01f, 1.5734662283e-01f, -6.0533065445e-02f, 2.8744838362e-02f, -1.4475653098e-02f, 7.3741983730e-03f, -3.2703655593e-03f, 1.3101771678e-03f},
        {4.4004415805e-01f, -6.7222706969e-01f, 3.1469324567e-01f, -1.2106613089e-01f, 5.7489676724e-02f, -2.8951306196e-02f, 1.4748396746e-02f, -6.5407311186e-03f, 2.6203543356e-03f},
        {3.1140768621e-01f, -5.2723264595e-01f, 3.2681622705e-01f, -1.6571616135e-01f, 8.2091070549e-02f, -4.1985522505e-02f, 2.1558021488e-02f, -9.6011245317e-03f, 3.8591478579e-03f},
        {2.3545411266e-01f, -4.2002230503e-01f, 2.9849192552e-01f, -1.7860012185e-01f, 9.8405575301e-02f, -5.2136865236e-02f, 2.7238498970e-02f, -1.2241573652e-02f, 4.9551740892e-03f},
        {1.8445053632e-01f, -3.3955276527e-01f, 2.6111274825e-01f, -1.7321025355e-01f, 1.0453147344e-01f, -5.8397411709e-02f, 3.1342038309e-02f, -1.4286182365e-02f, 5.8465512142e-03f},
        {1.4751535571e-01f, -2.7741009878e-01f, 2.2430319966e-01f, -1.5928934988e-01f, 1.0305050145e-01f, -6.0626267462e-02f, 3.3654414123e-02f, -1.5627206618e-02f, 6.4904010783e-03f},
        {1.1949382843e-01f, -2.2829289243e-01f, 1.9098724002e-01f, -1.4218308534e-01f, 9.6941146019e-02f, -5.9613663992e-02f, 3.4255504783e-02f, -1.6247499316e-02f, 6.8698231435e-03f},
        {9.7608450047e-02f, -1.8883237673e-01f, 1.6185826384e-01f, -1.2464644955e-01f, 8.8460078738e-02f, -5.6406223768e-02f, 3.3467341677e-02f, -1.6219692953e-02f, 6.9958126950e-03f},
      };
      for (int channel = 0; channel < para.spin_compress; ++channel) {
        for (int basis = 0; basis < basis_count; ++basis) {
          for (int pair = 0; pair < type_pairs; ++pair) {
            const int index = spin_offset +
              (channel * basis_count + basis) * type_pairs + pair;
            mu[index] = spin3_radial_frame[channel][basis];
            sigma[index] = para.sigma0 * descriptor_scale;
          }
        }
      }

      const int projection_offset =
        spin_offset + para.number_of_variables_descriptor_spin;
      const int channels = para.spin_compress;
      for (int leg = 0; leg < 4; ++leg) {
        for (int row = 0; row < channels; ++row) {
          for (int source = 0; source < channels; ++source) {
            const int index = projection_offset +
              (leg * channels + row) * channels + source;
            mu[index] = normal(rng) * spin_noise;
            if (source == (row + leg) % channels) {
              mu[index] += 1.0f;
            }
            sigma[index] = para.sigma0 * descriptor_scale;
          }
        }
      }
    } else {
      std::uniform_real_distribution<float> r1(0, 1);
      for (int n = 0; n < number_of_variables; ++n) {
        mu[n] = (r1(rng) - 0.5f) * 2.0f;
        sigma[n] = para.sigma0;
      }
    }
    if (curriculum_enabled) {
      for (int type = 0; type < para.num_types; ++type) {
        const int ann_offset = type * para.number_of_variables_ann_1;
        for (int neuron = 0; neuron < para.num_neurons1; ++neuron) {
          for (int descriptor = para.spin_order3_descriptor_start;
               descriptor < para.dim;
               ++descriptor) {
            mu[ann_offset + neuron * para.dim + descriptor] = 0.0f;
          }
        }
      }
    }
    // make sure the initial charges are zero
    if ((para.charge_mode || para.charge_vdw)) {
      const int num_part = (para.dim + 2) * para.num_neurons1;
      for (int t = 0; t < para.num_types; ++t) {
        for (int n = para.number_of_variables_ann_1 * t + num_part; n < para.number_of_variables_ann_1 * (t + 1); ++n) {
          mu[n] = 0.0f;
        }
      }
      mu[para.number_of_variables_ann_1 * para.num_types] = 2.0f; // make sure initial sqrt(epsilon_inf) > 0
    }
  } else {
    for (int n = 0; n < number_of_variables; ++n) {
      int count = fscanf(fid_restart, "%f%f", &mu[n], &sigma[n]);
      PRINT_SCANF_ERROR(count, 2, "Reading error for nep.restart.");
    }
    const int descriptor_offset = para.number_of_variables_ann * (para.train_mode == 2 ? 2 : 1);
#ifdef USE_CJ
    const int num_channels = para.num_types;
#else
    const int num_channels = para.num_types * para.num_types;
#endif
    descriptor_parameters_to_channel_major(
      mu.data(),
      descriptor_offset,
      num_channels,
      para.n_max_radial,
      para.n_max_angular,
      para.basis_size_radial,
      para.basis_size_angular);
    descriptor_parameters_to_channel_major(
      sigma.data(),
      descriptor_offset,
      num_channels,
      para.n_max_radial,
      para.n_max_angular,
      para.basis_size_radial,
      para.basis_size_angular);
    // flip the charges if needed
    if ((para.charge_mode || para.charge_vdw) && para.flip_charge) {
      const int num1 = (para.dim + 2) * para.num_neurons1;
      for (int t = 0; t < para.num_types; ++t) {
        int num2 = para.number_of_variables_ann_1 * (t + 1);
        if (para.charge_vdw) {
          num2 -= para.num_neurons1;
        }
        for (int n = para.number_of_variables_ann_1 * t + num1; n < num2; ++n) {
          mu[n] = -mu[n];
        }
      }
    }
    fclose(fid_restart);
  }
  gpuSetDevice(0); // normally use GPU-0
  gpu_mu.copy_from_host(mu.data());
  gpu_sigma.copy_from_host(sigma.data());
}

void SNES::initialize_mu_and_sigma_fine_tune(Parameters& para)
{
  // This map is needed because the foundation model misses 5 elements between H-Pu
  const int element_map[94] = {
    0,1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18,19,
    20,21,22,23,24,25,26,27,28,29,30,31,32,33,34,35,36,37,38,39,
    40,41,42,43,44,45,46,47,48,49,50,51,52,53,54,55,56,57,58,59,
    60,61,62,63,64,65,66,67,68,69,70,71,72,73,74,75,76,77,78,79,
    80,81,82,0,0,0,0,0,83,84,85,86,87,88
  };
  // read in the whole foundation file first
  const int NUM89 = 89;
  const int num_ann = NUM89 * para.number_of_variables_ann_1 + ((para.charge_mode || para.charge_vdw) ? 2 : 1);
#ifdef USE_CJ
  const int num_cnk_radial = NUM89 * (para.n_max_radial + 1) * (para.basis_size_radial + 1);
  const int num_cnk_angular = NUM89 * (para.n_max_angular + 1) * (para.basis_size_angular + 1);
#else
  const int num_cnk_radial = NUM89 * NUM89 * (para.n_max_radial + 1) * (para.basis_size_radial + 1);
  const int num_cnk_angular = NUM89 * NUM89 * (para.n_max_angular + 1) * (para.basis_size_angular + 1);
#endif
  const int num_tot = num_ann + num_cnk_radial + num_cnk_angular;
  std::vector<float> restart_mu(num_tot);
  std::vector<float> restart_sigma(num_tot);

  std::ifstream input(para.fine_tune_nep_restart);
  if (!input.is_open()) {
    std::cout << "Cannot open the foundation model file " << para.fine_tune_nep_restart << std::endl;
    exit(1);
  }
  std::vector<std::string> tokens;
    
  for (int n = 0; n < num_tot; ++n) {
    tokens = get_tokens(input);
    if (tokens.size() != 2) {
      std::cout << "Foundation model file should have two columns.\n";
      exit(1);
    }
    restart_mu[n] = get_double_from_token(tokens[0], __FILE__, __LINE__);
    restart_sigma[n] = get_double_from_token(tokens[1], __FILE__, __LINE__);
  }

  // get the required part
  int count = 0;
  for (int i = 0; i < para.num_types; ++ i) {
    int element_index = element_map[para.atomic_numbers[i] - 1];
    for (int j = 0; j < para.number_of_variables_ann_1; ++j) {
      mu[count] = restart_mu[element_index * para.number_of_variables_ann_1 + j];
      sigma[count] = restart_sigma[element_index * para.number_of_variables_ann_1 + j];
      ++count;
    }
  }
  count += (para.charge_mode || para.charge_vdw) ? 2 : 1; // the global parameters

#ifdef USE_CJ

  // radial descriptors
  for (int t2 = 0; t2 < para.num_types; ++t2) {
    int element_index_2 = element_map[para.atomic_numbers[t2] - 1];
    for (int n = 0; n <= para.n_max_radial; ++n) {
      for (int k = 0; k <= para.basis_size_radial; ++k) {
        int nk = n * (para.basis_size_radial + 1) + k;
        mu[count] = restart_mu[nk * NUM89 + element_index_2 + num_ann];
        if (para.fine_tune_descriptor) {
          sigma[count] = restart_sigma[nk * NUM89 + element_index_2 + num_ann];
        } else {
          sigma[count] = 0.0f;
        }
        ++count;
      }
    }
  }

  // angular descriptors
  for (int t2 = 0; t2 < para.num_types; ++t2) {
    int element_index_2 = element_map[para.atomic_numbers[t2] - 1];
    for (int n = 0; n <= para.n_max_angular; ++n) {
      for (int k = 0; k <= para.basis_size_angular; ++k) {
        int nk = n * (para.basis_size_angular + 1) + k;
        mu[count] = restart_mu[nk * NUM89 + element_index_2 + num_ann + num_cnk_radial];
        if (para.fine_tune_descriptor) {
          sigma[count] = restart_sigma[nk * NUM89 + element_index_2 + num_ann + num_cnk_radial];
        } else {
          sigma[count] = 0.0f;
        }
        ++count;
      }
    }
  }

#else

  // radial descriptors
  for (int t1 = 0; t1 < para.num_types; ++t1) {
    for (int t2 = 0; t2 < para.num_types; ++t2) {
      int element_index_1 = element_map[para.atomic_numbers[t1] - 1];
      int element_index_2 = element_map[para.atomic_numbers[t2] - 1];
      int t12 = element_index_1 * NUM89 + element_index_2;
      for (int n = 0; n <= para.n_max_radial; ++n) {
        for (int k = 0; k <= para.basis_size_radial; ++k) {
          int nk = n * (para.basis_size_radial + 1) + k;
          mu[count] = restart_mu[nk * NUM89 * NUM89 + t12 + num_ann];
          if (para.fine_tune_descriptor) {
            sigma[count] = restart_sigma[nk * NUM89 * NUM89 + t12 + num_ann];
          } else {
            sigma[count] = 0.0f;
          }
          ++count;
        }
      }
    }
  }

  // angular descriptors
  for (int t1 = 0; t1 < para.num_types; ++t1) {
    for (int t2 = 0; t2 < para.num_types; ++t2) {
      int element_index_1 = element_map[para.atomic_numbers[t1] - 1];
      int element_index_2 = element_map[para.atomic_numbers[t2] - 1];
      int t12 = element_index_1 * NUM89 + element_index_2;
      for (int n = 0; n <= para.n_max_angular; ++n) {
        for (int k = 0; k <= para.basis_size_angular; ++k) {
          int nk = n * (para.basis_size_angular + 1) + k;
          mu[count] = restart_mu[nk * NUM89 * NUM89 + t12 + num_ann + num_cnk_radial];
          if (para.fine_tune_descriptor) {
            sigma[count] = restart_sigma[nk * NUM89 * NUM89 + t12 + num_ann + num_cnk_radial];
          } else {
            sigma[count] = 0.0f;
          }
          ++count;
        }
      }
    }
  }

#endif

  input.close();
  gpuSetDevice(0); // normally use GPU-0
  gpu_mu.copy_from_host(mu.data());
  gpu_sigma.copy_from_host(sigma.data());
}

void SNES::calculate_utility()
{
  float utility_sum = 0.0f;
  for (int n = 0; n < population_size; ++n) {
    utility[n] = std::max(0.0f, std::log(population_size * 0.5f + 1.0f) - std::log(n + 1.0f));
    utility_sum += utility[n];
  }
  for (int n = 0; n < population_size; ++n) {
    utility[n] = utility[n] / utility_sum - 1.0f / population_size;
  }
}

void SNES::find_type_of_variable(Parameters& para)
{
  int offset = 0;

  // NN part
  int num_ann = (para.train_mode == 2) ? 2 : 1;
  for (int ann = 0; ann < num_ann; ++ann) {
    for (int t = 0; t < para.num_types; ++t) {
      for (int n = 0; n < para.number_of_variables_ann_1; ++n) {
        type_of_variable[n + offset] = t;
      }
      offset += para.number_of_variables_ann_1;
    }
    offset += (para.charge_mode || para.charge_vdw) ? 2 : 1; // the bias
  }

  // descriptor part
#ifdef USE_CJ
  const int num_radial_basis = (para.n_max_radial + 1) * (para.basis_size_radial + 1);
  for (int t1 = 0; t1 < para.num_types; ++t1) {
    for (int basis = 0; basis < num_radial_basis; ++basis) {
      type_of_variable[t1 * num_radial_basis + basis + offset] = t1;
    }
  }
  offset += num_radial_basis * para.num_types;
  const int num_angular_basis = (para.n_max_angular + 1) * (para.basis_size_angular + 1);
  for (int t1 = 0; t1 < para.num_types; ++t1) {
    for (int basis = 0; basis < num_angular_basis; ++basis) {
      type_of_variable[t1 * num_angular_basis + basis + offset] = t1;
    }
  }
#else
  const int num_radial_basis = (para.n_max_radial + 1) * (para.basis_size_radial + 1);
  for (int t1 = 0; t1 < para.num_types; ++t1) {
    for (int t2 = 0; t2 < para.num_types; ++t2) {
      int t12 = t1 * para.num_types + t2;
      for (int basis = 0; basis < num_radial_basis; ++basis) {
        type_of_variable[t12 * num_radial_basis + basis + offset] = t1;
      }
    }
  }
  offset += num_radial_basis * para.num_types * para.num_types;
  const int num_angular_basis = (para.n_max_angular + 1) * (para.basis_size_angular + 1);
  for (int t1 = 0; t1 < para.num_types; ++t1) {
    for (int t2 = 0; t2 < para.num_types; ++t2) {
      int t12 = t1 * para.num_types + t2;
      for (int basis = 0; basis < num_angular_basis; ++basis) {
        type_of_variable[t12 * num_angular_basis + basis + offset] = t1;
      }
    }
  }
  offset +=
    (para.n_max_angular + 1) * (para.basis_size_angular + 1) *
    para.num_types * para.num_types;
  if (para.spin_mode) {
    for (int channel = 0; channel < para.spin_compress; ++channel) {
      for (int basis = 0; basis <= para.spin_basis_size[0]; ++basis) {
        for (int t1 = 0; t1 < para.num_types; ++t1) {
          for (int t2 = 0; t2 < para.num_types; ++t2) {
            const int pair = t1 * para.num_types + t2;
            const int coefficient =
              (channel * (para.spin_basis_size[0] + 1) + basis) *
                para.num_types * para.num_types +
              pair;
            type_of_variable[offset + coefficient] = t1;
          }
        }
      }
    }
  }
#endif
}

void SNES::compute(Parameters& para, Fitness* fitness_function)
{

  print_line_1();
  if (para.prediction == 0) {
    printf("Started training.\n");
  } else {
    printf("Started predicting.\n");
  }

  print_line_2();

  if (para.prediction == 0) {

    if (para.train_mode == 0 || para.train_mode == 3) {
      if (!(para.charge_mode || para.charge_vdw)) {
        if (para.spin_mode) {
          printf(
            "%-8s%-11s%-11s%-11s%-11s%-11s%-11s%-11s%-11s%-11s%-11s%-11s%-11s%-11s\n",
            "Step",
            "Total",
            "L1Reg",
            "L2Reg",
            "E-Train",
            "F-Train",
            "V-Train",
            "M-Train",
            "T-Train",
            "E-Test",
            "F-Test",
            "V-Test",
            "M-Test",
            "T-Test");
        } else {
          printf(
            "%-8s%-11s%-11s%-11s%-13s%-13s%-13s%-13s%-13s%-13s\n",
            "Step",
            "Total-Loss",
            "L1Reg-Loss",
            "L2Reg-Loss",
            "RMSE-E-Train",
            "RMSE-F-Train",
            "RMSE-V-Train",
            "RMSE-E-Test",
            "RMSE-F-Test",
            "RMSE-V-Test");
        }
      } else {
        printf(
          "%-8s %-9s %-9s %-9s %-9s %-9s %-9s %-9s %-9s %-9s %-9s %-9s %-9s %-9s\n",
          "Step",
          "Total",
          "L1Reg",
          "L2Reg",
          "E-Train",
          "F-Train",
          "V-Train",
          "Q-Train",
          "Z-Train",
          "E-Test",
          "F-Test",
          "V-Test",
          "Q-Test",
          "Z-Test");
      }
    } else {
      printf(
        "%-8s %-11s %-11s %-11s %-13s %-13s\n",
        "Step",
        "Total-Loss",
        "L1Reg-Loss",
        "L2Reg-Loss",
        "RMSE-P-Train",
        "RMSE-P-Test");
    }
  }

  if (para.prediction == 0) {
    for (int n = 0; n < maximum_generation; ++n) {
      float curriculum_scale = 1.0f;
      if (curriculum_enabled) {
        const int epoch = n + 1;
        const int full_o3_epoch = std::max(2, 2 * maximum_generation / 3);
        const int warmup_end = std::max(1, full_o3_epoch / 2);
        if (epoch <= warmup_end) {
          curriculum_scale = 0.0f;
        } else if (epoch < full_o3_epoch) {
          curriculum_scale = static_cast<float>(epoch - warmup_end) /
            static_cast<float>(full_o3_epoch - warmup_end);
        }
        if (epoch == 1 || epoch == warmup_end || epoch == full_o3_epoch) {
          printf(
            "O3 curriculum generation %d: perturbation_scale=%.6f\n",
            epoch,
            curriculum_scale);
        }
      }
      create_population(curriculum_scale);
      fitness_function->compute(
        n, 
        para, 
        population.data(),
        fitness_energy.data(),
        fitness_force.data(),
        fitness_virial.data(),
        fitness_charge.data(),
        fitness_bec.data(),
        fitness_mforce.data(),
        fitness_tau.data(),
        fitness_spin_response.data());

      regularize_NEP4(para);

      sort_population(para);

      int best_index = index[para.num_types * population_size];
      fitness_function->report_error(
        para,
        n,
        fitness_total[para.num_types * population_size + 0], // already sorted, hence 0
        fitness_L1[para.num_types * population_size + best_index],
        fitness_L2[para.num_types * population_size + best_index],
        population.data() + number_of_variables * best_index);

      update_mu_and_sigma(curriculum_scale);
      if (0 == (n + 1) % para.output_interval) {
        const char* filename = "nep.restart";
        output_mu_and_sigma(para, filename);
      }
      // Optionally save the nep.restart file at the same time as save_potential
      if (0 == (n + 1) % para.save_potential && para.save_potential_restart) {
        std::string restart_file;
        fitness_function->get_save_potential_label(para, n, restart_file);
        restart_file += ".restart";
        output_mu_and_sigma(para, restart_file.c_str());
      }
    }
  } else {
    NepTxtHeader header;
    std::string error;
    if (!read_nep_txt_header("nep.txt", header, error)) {
      PRINT_INPUT_ERROR(error.c_str());
    }
    std::ifstream input("nep.txt");
    if (!input.is_open()) {
      PRINT_INPUT_ERROR("Failed to open nep.txt.");
    }
    std::vector<std::string> tokens;
    for (int n = 0; n < header.number_of_header_lines; ++n) {
      tokens = get_tokens(input);
    }
    for (int n = 0; n < number_of_variables; ++n) {
      tokens = get_tokens(input);
      population[n] = get_double_from_token(tokens[0], __FILE__, __LINE__);
    }
    const int descriptor_offset = para.number_of_variables_ann * (para.train_mode == 2 ? 2 : 1);
#ifdef USE_CJ
    const int num_channels = para.num_types;
#else
    const int num_channels = para.num_types * para.num_types;
#endif
    descriptor_parameters_to_channel_major(
      population.data(),
      descriptor_offset,
      num_channels,
      para.n_max_radial,
      para.n_max_angular,
      para.basis_size_radial,
      para.basis_size_angular);
    for (int d = 0; d < para.dim; ++d) {
      tokens = get_tokens(input);
      para.q_scaler_cpu[d] = get_double_from_token(tokens[0], __FILE__, __LINE__);
    }
    para.q_scaler_gpu[0].copy_from_host(para.q_scaler_cpu.data());
    fitness_function->predict(para, population.data());
  }
}

static __global__ void gpu_create_population(
  const int N,
  const int number_of_variables,
  const float* g_mu,
  const float* g_sigma,
  const int* g_curriculum_parameter,
  const float curriculum_scale,
  gpurandState* g_state,
  float* g_s,
  float* g_population)
{
  int n = blockIdx.x * blockDim.x + threadIdx.x;
  if (n < N) {
    int v = n % number_of_variables;
    gpurandState state = g_state[n];
    float s = gpurand_normal(&state);
    g_s[n] = s;
    const float scale =
      g_curriculum_parameter[v] ? curriculum_scale : 1.0f;
    g_population[n] = scale * g_sigma[v] * s + g_mu[v];
    g_state[n] = state;
  }
}

void SNES::create_population(float curriculum_scale)
{
  gpuSetDevice(0); // normally use GPU-0
  const int N = population_size * number_of_variables;
  gpu_create_population<<<(N - 1) / 128 + 1, 128>>>(
    N,
    number_of_variables,
    gpu_mu.data(),
    gpu_sigma.data(),
    gpu_curriculum_parameter.data(),
    curriculum_scale,
    curand_states.data(),
    gpu_s.data(),
    gpu_population.data());
  GPU_CHECK_KERNEL
  gpu_population.copy_to_host(population.data());
}

static __global__ void gpu_find_L1_L2_NEP4(
  const int number_of_variables,
  const int g_num_types,
  const int g_type,
  const int* g_type_of_variable,
  const float* g_population,
  float* gpu_cost_L1reg,
  float* gpu_cost_L2reg)
{
  int bid = blockIdx.x;
  int tid = threadIdx.x;
  __shared__ float s_cost_L1reg[1024];
  __shared__ float s_cost_L2reg[1024];
  s_cost_L1reg[tid] = 0.0f;
  s_cost_L2reg[tid] = 0.0f;
  for (int v = tid; v < number_of_variables; v += blockDim.x) {
    const float para = g_population[bid * number_of_variables + v];
    if ((g_type_of_variable[v] == g_type) && (g_type != g_num_types) || 
        (g_type_of_variable[v] != g_type) && (g_type == g_num_types))  {
      s_cost_L1reg[tid] += abs(para);
      s_cost_L2reg[tid] += para * para;
    }
  }
  __syncthreads();

  for (int offset = blockDim.x >> 1; offset > 0; offset >>= 1) {
    if (tid < offset) {
      s_cost_L1reg[tid] += s_cost_L1reg[tid + offset];
      s_cost_L2reg[tid] += s_cost_L2reg[tid + offset];
    }
    __syncthreads();
  }

  if (tid == 0) {
    gpu_cost_L1reg[bid] = s_cost_L1reg[0];
    gpu_cost_L2reg[bid] = s_cost_L2reg[0];
  }
}

void SNES::regularize_NEP4(Parameters& para)
{
  gpuSetDevice(0); // normally use GPU-0

  for (int t = 0; t <= para.num_types; ++t) {
    float num_variables = float(para.number_of_variables) / para.num_types;
    if (t == para.num_types) {
      num_variables = para.number_of_variables;
    }

    gpu_find_L1_L2_NEP4<<<population_size, 1024>>>(
      number_of_variables,
      para.num_types,
      t,
      gpu_type_of_variable.data(),
      gpu_population.data(),
      gpu_cost_L1reg.data(),
      gpu_cost_L2reg.data());
    GPU_CHECK_KERNEL

    gpu_cost_L1reg.copy_to_host(cost_L1reg.data());
    gpu_cost_L2reg.copy_to_host(cost_L2reg.data());

    for (int p = 0; p < population_size; ++p) {
      float cost_L1 = para.lambda_1 * cost_L1reg[p] / num_variables;
      float cost_L2 = para.lambda_2 * sqrt(cost_L2reg[p] / num_variables);
      fitness_total[p + t * population_size] =
        cost_L1 + cost_L2 + fitness_energy[p + t * population_size] +
        fitness_force[p + t * population_size] + fitness_virial[p + t * population_size] +
        fitness_charge[p + t * population_size] + fitness_bec[p + t * population_size] +
        fitness_mforce[p + t * population_size] + fitness_tau[p + t * population_size] +
        fitness_spin_response[p + t * population_size];
      fitness_L1[p + t * population_size] = cost_L1;
      fitness_L2[p + t * population_size] = cost_L2;
    }
  }
}

static void insertion_sort(float array[], int index[], int n)
{
  for (int i = 1; i < n; i++) {
    float key = array[i];
    int j = i - 1;
    while (j >= 0 && array[j] > key) {
      array[j + 1] = array[j];
      index[j + 1] = index[j];
      --j;
    }
    array[j + 1] = key;
    index[j + 1] = i;
  }
}

void SNES::sort_population(Parameters& para)
{
  for (int t = 0; t < para.num_types + 1; ++t) {
    for (int n = 0; n < population_size; ++n) {
      index[t * population_size + n] = n;
    }

    insertion_sort(
      fitness_total.data() + t * population_size,
      index.data() + t * population_size,
      population_size);
  }
}

static __global__ void gpu_update_mu_and_sigma(
  const int population_size,
  const int number_of_variables,
  const float eta_sigma,
  const int* g_type_of_variable,
  const int* g_curriculum_parameter,
  const float curriculum_scale,
  const int* g_index,
  const float* g_utility,
  const float* g_s,
  float* g_mu,
  float* g_sigma)
{
  const int v = blockIdx.x * blockDim.x + threadIdx.x;
  if (v < number_of_variables) {
    const int type = g_type_of_variable[v];
    float gradient_mu = 0.0f, gradient_sigma = 0.0f;
    for (int p = 0; p < population_size; ++p) {
      const int pv = g_index[type * population_size + p] * number_of_variables + v;
      const float utility = g_utility[p];
      const float s = g_s[pv];
      gradient_mu += s * utility;
      gradient_sigma += (s * s - 1.0f) * utility;
    }
    const float sigma = g_sigma[v];
    const float scale =
      g_curriculum_parameter[v] ? curriculum_scale : 1.0f;
    g_mu[v] += scale * sigma * gradient_mu;
    g_sigma[v] =
      fminf(sigma * exp(scale * eta_sigma * gradient_sigma), 1.0f);
  }
}

void SNES::update_mu_and_sigma(float curriculum_scale)
{
  gpuSetDevice(0); // normally use GPU-0
  gpu_index.copy_from_host(index.data());
  gpu_update_mu_and_sigma<<<(number_of_variables - 1) / 128 + 1, 128>>>(
    population_size,
    number_of_variables,
    eta_sigma,
    gpu_type_of_variable.data(),
    gpu_curriculum_parameter.data(),
    curriculum_scale,
    gpu_index.data(),
    gpu_utility.data(),
    gpu_s.data(),
    gpu_mu.data(),
    gpu_sigma.data());
  GPU_CHECK_KERNEL;
}

void SNES::output_mu_and_sigma(Parameters& para, const char* filename)
{
  gpuSetDevice(0); // normally use GPU-0
  gpu_mu.copy_to_host(mu.data());
  gpu_sigma.copy_to_host(sigma.data());
  std::vector<float> mu_file = mu;
  std::vector<float> sigma_file = sigma;
  const int descriptor_offset = para.number_of_variables_ann * (para.train_mode == 2 ? 2 : 1);
#ifdef USE_CJ
  const int num_channels = para.num_types;
#else
  const int num_channels = para.num_types * para.num_types;
#endif
  descriptor_parameters_to_basis_major(
    mu_file.data(),
    descriptor_offset,
    num_channels,
    para.n_max_radial,
    para.n_max_angular,
    para.basis_size_radial,
    para.basis_size_angular);
  descriptor_parameters_to_basis_major(
    sigma_file.data(),
    descriptor_offset,
    num_channels,
    para.n_max_radial,
    para.n_max_angular,
    para.basis_size_radial,
    para.basis_size_angular);
  FILE* fid_restart = my_fopen(filename, "w");
  for (int n = 0; n < number_of_variables; ++n) {
    fprintf(fid_restart, "%15.7e %15.7e\n", mu_file[n], sigma_file[n]);
  }
  fclose(fid_restart);
}
